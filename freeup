#!/usr/bin/perl -w
use strict;

use bigint;
use open ':encoding(utf8)';

use Fcntl;
use File::Basename;
use File::Find;
use Getopt::Long;
use NDBM_File;
use Text::CSV;

my %file_history; # (filename => last timestamp when we asked the user about it)

my @file_score; # array of [filename, size, examined_desc, ctime] arrayrefs

my $best_size = 0;  # Size of best range so far

my $best_index = undef; # Index of beginning of best range so far
my $best_end_index = undef; # Index of end (exclusive) of best range so far

my $best_index_desc = undef;
my $best_end_index_desc = undef;

my $this_index = 0; # Index of beginning of current range
my $this_size = 0; # Size of current range

my $index = 0;

my $start_time = time(); # Timestamp of when script started running

my $topdev; # Device number of top level directory

my $verbose = 0;

################################
# Dealings with list of files and details
################################

# Callback for File::Find
sub wanted
{
    my ($dev, $ino, $mode, $nlink, $uid, $gid, $rdev, $size,
	$atime, $mtime, $ctime, $blksize, $blocks) = lstat($File::Find::name);

    if (-f $_)
    {
	push @file_score, [$File::Find::name,
			   $size + $blksize,
			   'Unchecked',
			   $ctime];
    }
    elsif (-d $_)
    {
	$File::Find::prune = 1 if ($dev != $topdev);
    }
}

sub scan_dirs
{
    my @dirs = @_;
    for my $dir (@dirs)
    {
	my ($dev,$ino,$mode,$nlink,$uid,$gid) = lstat($dir);
	$topdev = $dev;

	find(\&wanted, $dir);
    }
}

sub load_dir_cache
{
    my @dirs = @_;

    my $csv = Text::CSV->new({binary => 1});
    open my $fh, '<', "$ENV{HOME}/.freeup/dir_cache" or return 0;

    my $row = $csv->getline($fh) or return 0;
    $row->[0] eq 'Version' or die "Expected 'Version', got '$row->[0]'";
    $row->[1] eq '1' or return 0;

    $row = $csv->getline($fh) or return 0;
    $row->[0] eq 'Dirs' or die "Expected 'Dirs', got '$row->[0]'";
    $row->[1] eq join(', ', @dirs) or return 0;

    $row = $csv->getline($fh) or return 0;
    $row->[0] eq 'Timestamp' or die "Expected 'Timestamp', got '$row->[0]'";
    $row->[1] > $start_time - 60 * 60 or return 0;

    while($row = $csv->getline($fh))
    {
	push @file_score, $row;
    }

    return 1;
    # Filehandle automatically closed, either at this point or earlier
    # with 'return 0'
}

sub save_dir_cache
{
    my @dirs = @_;

    my $csv = Text::CSV->new({binary => 1, eol => "\n"});
    open my $fh, '>', "$ENV{HOME}/.freeup/dir_cache"
	or die "Can't save dir cache: $!";

    $csv->print($fh, ['Version', 1]);
    $csv->print($fh, ['Dirs', join(', ', @dirs)]);
    $csv->print($fh, ['Timestamp', $start_time]);

    foreach (@file_score)
    {
	$csv->print($fh, $_);
    }

    close $fh or die "Can't write dir cache: $!";
}

################################
# Dealings with scanning list of files and determining best range
################################

sub find_file_in_best_range
{
    ### Scan all files

    for my $score (@file_score)
    {
	my ($filename, $size, $examined_desc, $ctime) = @$score;

	my $this_history = $file_history{$filename};
	my $should_break_range = 0;

	if ($examined_desc eq 'Unexamined')
	{
	    # We know we don't need to check the file history; based
	    # on the dir cache, it's unexamined. Having this case
	    # allows us to greatly reduce the number of times we
	    # consult %file_history (which is costly, since it's a
	    # tied hash).
	}
	elsif ($this_history = $file_history{$filename})
	{
	    # We've shown this file before. We need to decide
	    # stochastically if we've shown it recently enough that we
	    # should break the range we're currently adding up.

	    my $examined_time = $this_history - $ctime;
	    my $unexamined_time = $start_time - $this_history;

	    # Cap unexamined time at 6 months, so that regardless of
	    # age we present files every so often without waiting
	    # years
	    if ($unexamined_time > 60*60*24*30*6) {
		$unexamined_time = 60*60*24*30*6;
	    }
	    
	    $examined_desc = int($unexamined_time * 100 /
				 ($examined_time + $unexamined_time))
		. '% expired';

	    if ($examined_time > 0 and
		$unexamined_time > 0 and
		rand($examined_time + $unexamined_time) < $examined_time)
	    {
		# We've examined this file somewhat recently, so count
		# the range that contains it as two smaller ranges of
		# unexamined files (i.e. "break" the range into two)
		$should_break_range = 1;
	    }
	    else
	    {
		# We just "expired" a file that used to break the
		# range, but hasn't been examined in a while. Treat it
		# as unexamined again. This also happens under some
		# odd "can't happen" conditions (if one of the times
		# is negative).
		undef $file_history{$filename};
		$should_break_range = 0;
	    }
	}
	else
	{
	    # Examining the file history just determined that this
	    # file is unexamined. Mark it so.
	    $examined_desc = 'Unexamined';
	}

	if ($should_break_range)
	{
	    # We want to stop the current range, and start a new one
	    # after the current point.
	    
	    if ($this_size > $best_size)
	    {
		# We've broken the record for the best range
		$best_index = $this_index;
		$best_end_index = $index;
		$best_size = $this_size;

		if ($best_index == 0)
		{
		    $best_index_desc = "Beginning of scan";
		}
		else
		{
		    $best_index_desc = "$file_score[$best_index-1][0] ("
			. human_filesize($file_score[$best_index-1][1])
			. ", $file_score[$best_index-1][2])";
		}

		$best_end_index_desc = "$filename ("
		    . human_filesize($size)
		    . ", $examined_desc)";
	    }

	    # Reset everything, we start a new range on the next item
	    $this_index = $index + 1;
	    $this_size = 0;
	}
	else
	{
	    # We're just continuing the current range
	    $this_size += $size;
	}
	
	$index += 1;
	$score->[2] = $examined_desc;
    }

    ### Finish file scanning

    if ($this_size > $best_size)
    {
	$best_index = $this_index;
	$best_end_index = $index;
	$best_size = $this_size;

	if ($best_index == 0)
	{
	    $best_index_desc = "Beginning of scan";
	}
	else
	{
	    $best_index_desc = "$file_score[$best_index-1][0] ("
		. human_filesize($file_score[$best_index-1][1])
		. ", $file_score[$best_index-1][2])";
	}

	$best_end_index_desc = "End of scan";
    }

    ### Find file in best range

    my $remaining_size = int(rand($best_size)) + 1;
    for my $index ($best_index .. $best_end_index-1)
    {
	$remaining_size -= $file_score[$index][1];
	return $index if ($remaining_size <= 0)
    }

    die "Can't happen";
}

# Convert a file size to a friendly representation
sub human_filesize
{
    my ($size) = @_;
    if ($size > 1024*1024*1024)
    {
	return int($size/1024/1024/1024) . "G";
    }
    elsif ($size > 1024*1024)
    {
	return int($size/1024/1024) . "M";
    }
    elsif ($size > 1024)
    {
	return int($size/1024) . "K";
    }
    else
    {
	return $size;
    }
}

GetOptions('verbose' => \$verbose);

die "Need to specify target directory" unless @ARGV;

mkdir "$ENV{HOME}/.freeup";
tie(%file_history,
    'NDBM_File',
    "$ENV{HOME}/.freeup/file_history",
    O_RDWR | O_CREAT,
    0666)
    or die "$0: Tie failed: $!";

print "Scanning\n" if $verbose;

unless (load_dir_cache(@ARGV))
{
    scan_dirs(@ARGV);
    save_dir_cache(@ARGV);
}

print "Finding\n" if $verbose;

my $best_midpoint = find_file_in_best_range();
my $best_file = $file_score[$best_midpoint][0];
my $best_dir = dirname($best_file);

print "Printing\n" if $verbose;

if (!$verbose)
{
    print "$file_score[$best_midpoint][0]\n";
}
else
{
    print "Best range is $best_index, end $best_end_index, out of $index\n";
    print "Range begin: $best_index_desc\n";
    print "Range end: $best_end_index_desc\n";
    print "Range size: ", human_filesize($best_size), "\n";
    print "Chosen file is $best_midpoint: $file_score[$best_midpoint][0]\n";
    print human_filesize($file_score[$best_midpoint][1]), 
        "  $file_score[$best_midpoint][0]\n";

    while($best_dir ne '/')
    {
	my $best_dir_size = 0;
	for my $file_iter (@file_score)
	{
	    my $name = $file_iter->[0];
	    
	    my $iter_dir = substr($name, 0, length($best_dir)+1);
	    if ($iter_dir eq "$best_dir/")
	    {
		$best_dir_size += $file_iter->[1];
	    }
	}
	
	print human_filesize($best_dir_size), "  $best_dir/\n";
	
	$best_dir = dirname($best_dir);
	#print "  Testing $best_dir\n";
	#print "    ", substr($file_score[$best_index][0], 0, length($best_dir)+1), " <=> ", "$best_dir/", "\n";
	#print "    ", substr($file_score[$best_end_index][0], 0, length($best_dir)+1), " <=> ", "$best_dir/", "\n";
	last if $best_end_index >= $index;
	last if substr($file_score[$best_index][0], 0, length($best_dir)+1) eq "$best_dir/" and
	    substr($file_score[$best_end_index][0], 0, length($best_dir)+1) eq "$best_dir/";
	#print "    passed\n";
    }
}

print "Finishing\n" if $verbose;

$file_history{$best_file} = time();

untie(%file_history);
